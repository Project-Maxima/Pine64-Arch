# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=5.27.10
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=(desktop-file-utils
         libphonenumber
         kirigami-addons5
         kpeople5
         modemmanager-qt5
         networkmanager-qt5
         plasma-wayland-session
         plasma-workspace
         pipewire-session-manager)
makedepends=(baloo5
             cmake
             extra-cmake-modules
             kdoctools5)
replaces=('plasma-phone-components')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha512sums=('ccd88e89137eca3d0a03f771be48b033a96d4c13ac174c4cdd94e83c6aaf1f424f68f0ca99a15f2ea1c5c7b4b99d6297dde3ba34018411be6424969aa9941684')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
