# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>

pkgname=alligator
pkgver=21.08
pkgrel=1
pkgdesc="RSS Feed Reader"
arch=('x86_64' 'aarch64')
url="https://www.plasma-mobile.org/"
license=('GPL-3.0-or-later')
depends=(kcoreaddons ki18n kconfig syndication kirigami2)
makedepends=('cmake' 'extra-cmake-modules')
source=("http://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('9e22b461f4c13959bb1f28a26d24e780')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}

