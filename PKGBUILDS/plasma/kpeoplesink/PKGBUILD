# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=kpeoplesink
pkgver=r77.g5d5e1b9
pkgrel=1
pkgdesc="Expose Sink contacts to KPeople"
arch=('aarch64' 'x86_64')
url="https://invent.kde.org/pim/$pkgname"
license=('LGPL')
depends=('kaccounts-integration' 'kcontacts' 'kpeople' 'sink')
makedepends=('git' 'cmake' 'extra-cmake-modules')
replaces=("$pkgname-git")
source=("git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
