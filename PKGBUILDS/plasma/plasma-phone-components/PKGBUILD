# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phone-components
pkgver=5.23.2
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'ofono' 'libqofono-qt5' 'kpeople' 'telepathy-qt' 'libphonenumber' 'plasma-wayland-session' 'pipewire-media-session')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
source=("https://invent.kde.org/plasma/plasma-phone-components/-/archive/v${pkgver}/plasma-phone-components-v${pkgver}.tar.gz"
        'revert-modem-manager.patch')
install=$pkgname.install
sha512sums=('b1423dd2d54df35e050880e73d3c9d6033bb708c14b6e881001c2e7faeffc0fc6a1aadf613fdf95cb03243ab75745232ca5045d23f2bbc52b19393a0df6ee13f'
            '768ef03d8c2f216ec837cd3cbaccbe0413ed4ed53b556b9d458b6350735cccef94034a09b5d4cadaab8f8ae3bdec043fc7b0d92740e923bb49acbc20fb1e1789')

prepare() {
  cd "$pkgname-v${pkgver}"

  # Revert modem manager patch, this can be added back in when it's done
  patch -p1 < ../revert-modem-manager.patch

  cd "$srcdir"

  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v${pkgver} \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
