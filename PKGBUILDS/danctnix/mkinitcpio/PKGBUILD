# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

# DANCTNIX: - Force privacy screen inclusion to return 0 even the kernel does not have it

pkgname=mkinitcpio
pkgver=37.3
pkgrel=1.1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
checkdepends=('bats' 'bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch'
        'test-cases-bash-completion-Use-Linux-banner-for-non-.patch')
install=mkinitcpio.install
sha512sums=('edee8dfbc452a0fc6ca9e8c7321beffa4463e504641b99a8d08ab9df665b697a0192f7760994b9917f34ba83d15543dedd3c4690364663b4534724a06a262db7'
            'SKIP'
            '24f8431b23ee039fc1aee7b10482d310a8fe37a36975e83aa31d3d0d35d12973c1397057876425fe712be25e999b10753ae05613f3900eccfe5af47e86eda202'
            'e7c14829c0b5c1330819bfef00923f92a1eb59f6d9cf41b8ac421dc970497b9c75e093b37a41f63aa75f9c11ba3ac72c5385d3a00c6a7686cfda82a1a9ad1883'
            'd16c37550da527ef1ca1451c9c81c52197f10a30dfa91aa434a55562f06d4ac97c90f1cb3008b1ba612d6ccf3a46683e001a6e2ba04088d2169274e561183f24'
            'aa7dad19bd6d13587a4d246765cded79b2da97e72c686daffc6eba3539dee1ccf66f3bcc3de8ffce18482a454cb96519b1f2656ea0eb1d1edf24d1b8f2696fe7')
b2sums=('f903348aa23983b4de1e095420acc358a09a6fd0c8ed9a9a7b9bd7df0bfde0bb17869996759f8f3c73ada85dd3ee3f83958401e1f9fa15aaacc06b048fe13660'
        'SKIP'
        'f62db2646bb8828de66e73ea479b2276bba970c5338451e2df4122012d42f453e6fc6892702a58cf36b94f759a8ed7d2f65782067922578d81ae1aaa8cc78806'
        'e0dc41210fb7b811624758022b4254c396d243460e63c86358fe46dc18510a39e021ff9c53944dffb8ec5fbd37681c47f4f83714b63751b7a57d7d9710f47cca'
        '107ef9ee2623a3634a31d62b49efc316615e4c34c847f27a55886c93b2bdb87020bcd7f513ab922de730fe4a6d3bffd26a82180467c14c0993c603bc8801b5df'
        '8c720fc8111cd54215356dae93fa32f0416e3a50da4a84c09d1230d1476561ad2592c59a8e7e945c71303d04b05314f09403980bca3eb019748706b49ff0f0c5')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
  patch -p1 -i ../test-cases-bash-completion-Use-Linux-banner-for-non-.patch

  # Since we do not have privacy screen modules enabled, there's no need for this
  # it also workaround an error when generating initramfs.
  sed -i '/add_checked_modules_from_symbol/ s/\([^ ].*drm_privacy_screen_register.*\)/# \1/' \
    install/kms
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
