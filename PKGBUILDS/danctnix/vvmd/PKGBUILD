# Maintainer: Aren Moynihan <aren@peacevolution.org>
pkgname=vvmd
pkgver=0.6.1
pkgrel=1
pkgdesc="Visual Voicemail Daemon"
url="https://gitlab.com/kop316/vvmd"
arch=('x86_64' 'aarch64')
license=("GPL2")
install=vvmd.install
depends=("mobile-broadband-provider-info" "libmm-glib" "libphonenumber")
makedepends=("meson")
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/kop316/vvmd/-/archive/$pkgver/vvmd-$pkgver.tar.gz"
        "vvmd.service")
sha256sums=('a35eea3216b2103f1570e1eee8f1fb32d553b0799d5c7832bbd8054eebd41868'
            'b4bfd3b3758427975eee8ca5b4a39fb43ce7ea36b56d7c82a23fdacdb4161c50')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "vvmd.service" "$pkgdir/usr/lib/systemd/user/vvmd.service"
}
