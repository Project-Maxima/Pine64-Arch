# Maintainer: Danct12 <danct12@disroot.org>
pkgname=anbox-image-aarch64
pkgver=0_20200520
pkgrel=7
pkgdesc="Anbox image for ARM64"
arch=(aarch64)
url="https://anbox.postmarketos.org"
license=('custom')
depends=(anbox)
makedepends=(squashfs-tools)
provides=(anbox-image)
_fdroidver=1013050
_fdroid="org.fdroid.fdroid_$_fdroidver.apk"
_fdroidprivver=2120
_fdroidpriv="org.fdroid.fdroid.privileged_$_fdroidprivver.apk"
source=(android.img::http://anbox.postmarketos.org/android-7.1.2_r39-anbox_arm64-userdebug.img
        https://f-droid.org/repo/$_fdroid
        https://f-droid.org/repo/$_fdroidpriv
        SimpleKeyboard.apk::https://f-droid.org/repo/rkr.simplekeyboard.inputmethod_88.apk
        network-fix.patch
        display_settings.xml)

prepare() {
  mkdir -p ./squashfs
  # Decompressing the image
  unsquashfs -f -d ./squashfs android.img

  cd ./squashfs

  patch -p1 -N < ../network-fix.patch

  # remove su, it's broken and less apps will refuse to start
  rm -v system/xbin/su

  # Install FDroid
  mkdir system/app/FDroid
  mkdir system/priv-app/FDroid
  # install -Dm 0644 doesn't work as it copies the symlink, hence cp
  cp -L "$srcdir"/$_fdroid system/app/FDroid/$_fdroid
  cp -L "$srcdir"/$_fdroidpriv system/priv-app/FDroid/$_fdroidpriv
  chmod 0644 system/app/FDroid/$_fdroid
  chmod 0644 system/priv-app/FDroid/$_fdroidpriv

  # Install SimpleKeyboard
  mkdir system/app/SimpleKeyboard
  cp -L "$srcdir"/SimpleKeyboard.apk system/app/SimpleKeyboard/SimpleKeyboard.apk
  chmod 0644 system/app/SimpleKeyboard/SimpleKeyboard.apk
}

package() {
  mksquashfs "$srcdir"/squashfs "$srcdir"/android-fdroid.img -comp xz -no-xattrs
  install -Dm600 "$srcdir"/android-fdroid.img "$pkgdir"/var/lib/anbox/android.img

  # display overscan
  install -Dm666 "$srcdir"/display_settings.xml "$pkgdir"/var/lib/anbox/data/system/display_settings.xml
}

md5sums=('5c2e2a5fc1e6a388f6949b3df65b5c0c'
         'f6835b57edc6110119c7140fb080c294'
         'dc7a163cc47fd2a2c441a266df06e69b'
         '3564709bff0cb75c7e3b8d272bd86a6e'
         '1ef3b59fb14bbb7d17236af393d27474'
         '363e812b6efcaf579da46affc7cd321f')
