# Maintainer: Danct12 <danct12@disroot.org>
pkgbase=danctnix-tweaks-app
pkgname=("$pkgbase" "$pkgbase-phosh" "$pkgbase-sxmo" "$pkgbase-pinephone")
pkgver=0.12.0+23+ge52235e
pkgrel=1
pkgdesc="Tweaks tool for phone UIs"
url="https://github.com/dreemurrs-embedded/danctnix-tweaks"
license=('LGPL')
arch=('any')
makedepends=(
    'git'
    'gtk3'
    'libhandy'
    'meson'
    'python-gobject'
    'python-yaml'
    'python-setuptools'
)
_commit="e52235e1482da1b735933a44684a2dff9e61fdeb" # branch/master
source=("git+https://github.com/dreemurrs-embedded/danctnix-tweaks.git#commit=$_commit")

pkgver() {
    cd danctnix-tweaks
    git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
    arch-meson danctnix-tweaks output
    meson compile -C output
}

package_danctnix-tweaks-app() {
    depends=(
        'gtk3'
        'libhandy'
        'python-gobject'
        'python-yaml'
    )
    install=danctnix-tweaks-app.install

    DESTDIR="$pkgdir" meson install -C output
    mv "$pkgdir"/usr/share/danctnix-tweaks/phosh.yml "$srcdir"/phosh.yml
    mv "$pkgdir"/usr/share/danctnix-tweaks/pinephone.yml "$srcdir"/pinephone.yml
    mv "$pkgdir"/usr/share/danctnix-tweaks/sxmo-x11.yml "$srcdir"/sxmo-x11.yml

    # Not yet ready, we need the user to manually mkinitcpio after the config changes.
    rm "$pkgdir"/usr/share/danctnix-tweaks/unlocker.yml
}

package_danctnix-tweaks-app-phosh() {
     depends=('danctnix-tweaks-app' 'phosh')

     mkdir -p "$pkgdir"/usr/share/danctnix-tweaks
     mv "$srcdir"/phosh.yml "$pkgdir"/usr/share/danctnix-tweaks/phosh.yml
}

package_danctnix-tweaks-app-sxmo() {
     depends=('danctnix-tweaks-app')

     mkdir -p "$pkgdir"/usr/share/danctnix-tweaks
     mv "$srcdir"/sxmo-x11.yml "$pkgdir"/usr/share/danctnix-tweaks/sxmo-x11.yml
}

package_danctnix-tweaks-app-pinephone() {
     depends=('danctnix-tweaks-app')

     mkdir -p "$pkgdir"/usr/share/danctnix-tweaks
     mv "$srcdir"/pinephone.yml "$pkgdir"/usr/share/danctnix-tweaks/pinephone.yml
}

md5sums=('SKIP')
