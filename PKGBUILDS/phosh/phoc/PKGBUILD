# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.9.0
pkgrel=1
pkgdesc="A compositor for phones based on wlroots"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phoc"
license=('GPL')
depends=('gobject-introspection' 'gnome-desktop' 'libinput' 'mutter'
         'xcb-util-errors' 'xcb-util-wm' 'wayland-protocols')
makedepends=('meson' 'ninja')

# We don't want to maintain the wlroots package, so we static compile it
_wlroots_commit="5413b1ec61c6e3390929db595c0ec92f92ea2594"
source=(https://gitlab.gnome.org/World/Phosh/phoc/-/archive/v$pkgver/phoc-v$pkgver.tar.gz
        https://source.puri.sm/Librem5/wlroots/-/archive/${_wlroots_commit}/wlroots-${_wlroots_commit}.tar.gz)

prepare() {
	rm -r ${pkgname}-v${pkgver}/subprojects/wlroots
	mv wlroots-${_wlroots_commit} ${pkgname}-v${pkgver}/subprojects/wlroots
}

build() {
	arch-meson ${pkgname}-v${pkgver} output -Dtests=false \
		-Dwlroots:logind-provider=systemd \
		-Dwlroots:libseat=disabled \
		-Dembed-wlroots=enabled \
		--default-library=static
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	# Install scale-to-fit helper
	install -Dm755 ${pkgname}-v${pkgver}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

	# Remove unnecessary files
	rm -r "$pkgdir"/usr/lib
	rm -r "$pkgdir"/usr/include
}

md5sums=('d86e00b631af3af5ed127bfd898ad909'
         '65de7563a03281e822de0e5452ee851d')
