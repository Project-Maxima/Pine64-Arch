# Contribotor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>
# Maintainer: Danct12 <danct12@disroot.org>

pkgname=gnome-software-mobile
_pkgname=gnome-software
pkgver=41.4
pkgrel=1
pkgdesc="GNOME Software Tools (w/ Purism patches)"
url="https://gitlab.gnome.org/GNOME/gnome-software"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
makedepends=(appstream gsettings-desktop-schemas libpackagekit-glib flatpak fwupd
             docbook-xsl git gobject-introspection gspell gtk-doc meson valgrind
             gnome-online-accounts libxmlb malcontent libhandy)
_commit=c6ab7cfd7b022b1dde128b12e873f79dad870033 # tags/41.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-software.git#commit=$_commit"
        0001-assets-Make-the-up-to-date-picture-fit-phones.patch
        0002-Add-GsInfoWindow.patch
        0003-metered-data-dialog-Make-it-a-GsInfoWindow-and-a-Hdy.patch
        0004-updates-page-Move-Checking-from-the-headerbar-to-an-.patch
        0005-details-page-Make-the-loading-status-fit-phones.patch
        0006-app-details-page-Drop-the-hardcoded-label-width.patch
        0007-data-List-compatible-form-factors.patch
        0008-icon-Ensure-icon-files-exist.patch
        0009-app-Ensure-themed-icons-exist.patch
        0010-app-Be-more-strict-with-icon-scales.patch
        0011-style-Turn-category-tile-padding-into-child-margin.patch
        0012-style-Fix-the-iconless-category-tile-min-width.patch
        0013-moderate-page-Use-a-HdyStatusPage.patch
        0014-progress-button-Ellipsize-the-label.patch
        0015-updates-section-Ellipsize-the-button-label.patch
        GsUpdatesSection-Always-present-Download-button-before-Re.patch)
sha256sums=('SKIP'
            'e7c70b6a879e1dd273b34524312d58bdc7569399c1c06d3e487c3d6ba83631d4'
            '9eb25cec0a60f80d03bb9d88cfd169092f6f4662019413d3894e4fc5c8e4ce37'
            'c0f239a3188e8daa011d90afd4fdf8405508a16ce4ad5a91dff96f15ff66de26'
            '222f3270a864fdde508643cce847718726f30445186e86dea0178bc8dd18b09c'
            'df214b976695a68ae87836dda92dc7cf5a050e72eb8033d903262e5a65bb6824'
            '04f09e48ba267fb84c377eaaec09cc3b717287fcec49d37bc8c58ee30d640641'
            'a0ee9bf30a8859ebbbd212c44e73855e33d80b3fc60f8a2e79e94c438ba07496'
            'a5c36efec40ad6ef955ef3a7e7e5d2dd610ec4b3f7a57dbcc016c22003bc5942'
            '6e01d9278bc58c1b494a2964012972308377e8f1a175453ae398bfe0aca40116'
            'e629242b2472f7962fe8d18390babc33bf6ca40ba3ea17389181b97ce9dc84f6'
            '7b1fe890f07f07770b0449af8919da596d1794cd83f19ee15c28db9360ca4882'
            'f049c68ea750816edecf73d84b5b9aed3f999cd821217b27994db1e17266e8eb'
            '286d6e366f832048aee782f6dc1fe59ff2270eb15a2b40f28ffe4c22d8186697'
            'c5ab412e5f6f5f640531381861d4eb1c6a212491eb7a4270a41df6c8e9d1ae6c'
            '17ba8a3eea29b53d0279c6d74ff43d10060749769edb9df8f24500c57e6d44b9'
            'b0f939c4bc127d5151fc3b23c252367edb50e94fcead7d22dee3c76d5c6d2a24')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build -Dsysprof=disabled
  meson compile -C build
}

check() {
  # build container troubles
  meson test -C build --print-errorlogs || :
}

package() {
  provides=(gnome-software gnome-software-packagekit-plugin)
  conflicts=(gnome-software gnome-software-packagekit-plugin)
  depends=(libxmlb gsettings-desktop-schemas gspell libpackagekit-glib
           gnome-online-accounts appstream libhandy archlinux-appstream-data packagekit)
  optdepends=('flatpak: Flatpak support plugin'
              'fwupd: fwupd support plugin'
              'malcontent: Parental control plugin')

  meson install -C build --destdir "$pkgdir"
}
