# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=gnome-control-center
pkgname=gnome-control-center-mobile
pkgver=40.1
pkgrel=1
pkgdesc="GNOME's main interface to configure various aspects of the desktop - Purism fork"
url="https://source.puri.sm/Librem5/gnome-control-center"
license=(GPL2)
arch=(x86_64 armv7h aarch64)
depends=(accountsservice cups-pk-helper gnome-bluetooth gnome-desktop
         gnome-online-accounts gnome-settings-daemon gsettings-desktop-schemas gtk3
         libgtop nm-connection-editor sound-theme-freedesktop upower libpwquality
         gnome-color-manager smbclient libmm-glib libgnomekbd grilo libibus
         libgudev bolt udisks2 libhandy gsound colord-gtk modemmanager)
makedepends=(docbook-xsl git python meson)
checkdepends=(python-dbusmock python-gobject xorg-server-xvfb)
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: WebDAV file sharing'
            'gnome-remote-desktop: screen sharing'
            'rygel: media sharing'
            'openssh: remote login')
provides=(gnome-control-center)
conflicts=(gnome-control-center)
_commit=32d3bb2d188143cf28d3dc5e22347638877e3957
source=("git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        # purism
        0001-Add-helper-for-new-connection-editor.patch
        0001-Add-new-connection-editor.patch
        0002-wifi-Use-new-connection-editor.patch
        0020-user-panel-Hide-automatic-login-switch.patch
        0002-UserAccount-fix-window-resizing-issue.patch
        wwan-Add-new-panel-for-modem-management.patch
        0001-avatar-chooser-Adapt-to-work-on-librem5.patch
        0002-datetime-Fix-timezone-selection-map.patch
        0003-display-Request-a-smaller-size-for-arrangment-widget.patch
        0004-online-accounts-Make-edit-account-dialog-resizable.patch
        0005-power-show-brightness-slider-in-a-separate-row.patch
        0006-sound-fix-test-dialog-on-small-screen.patch
        0007-UserAccount-Allow-stack-to-have-different-size.patch
        Add-patches-to-check-if-phone.patch
        display-Let-button-box-have-vertical-orientation-on-.patch
        notification-Hide-lockscreen-notification-option-on-.patch
        shell-Hide-some-panels-on-phones.patch
        user-accounts-Constrain-passwords-to-digits-on-phone.patch
        user-panel-hide-add-user-unlock-buttons.patch
        Add-form-factors-to-desktop-and-appdata-files.patch
        # mobian
        0002-search-ellipsize-button-label-to-fit-small-screens.patch
        0003-usage-ellipsize-labels-to-fit-small-screens.patch
        0004-power-add-more-suspend-timing-options.patch
        0005-applications-ellipsize-title-label-to-always-fit-sma.patch
        fix-40-wifi-panel.patch
        fix-region-panel-width.patch
        fix-keyboard-panel-width.patch
        # backport
        display-config-Use-G_APPROX_VALUE-to-compare-scale-double.patch
        display-config-Expose-the-supported-scales-as-a-reffed-GA.patch
        display-config-Filter-invalid-modes-and-scales-when-setti.patch
        display-settings-Show-scaling-values-using-a-combo-box-wh.patch
        display-Prevent-snapping-unmodified-display.patch
        display-deprecation-fix.patch
        shell-Mirror-the-main-leaflet-s-folded-property.patch
        display-Use-ComboBox-when-folded.patch)
sha256sums=('SKIP'
            'SKIP'
            '265dad36120c4527cd14ac19a5bd71a679ef6e0843545ee256425bd2ee173ca0'
            'a55fe23c3fa8e403650ffc4b4c360d3acec2b8d9ffd5df9db8762d4f1fee4293'
            '51b5002710f86b6f7e72c9ec15c68299db458e0f7a5c9ab323e0f5a58252af4e'
            'b31a564f8f65354b4e8bcc4eeeac2a0493cee8cd239652cbf30086a8ae5f4eb4'
            'c8348853ab185181f882cfae8910b9161888d6eebcb15b9301fc43516690e9c3'
            '876bd292522600a831a8192c35e3dfeb01fdb75fc2b492f62c7fa7afed506970'
            'bb6521472ea920f7c5fe5df0d080cf32b4b877419d6e7677202c515f74d7bb3f'
            'fafec01605d291b2bf65d44cde3a047cd622b2be8ecc886011eb52cc9418c60f'
            '5aaa69722f0c41e4249dd8a34f6931b3db24ca73f6326aa5bc57fa881b2cfe44'
            '3d441629c4f45efbfd179182559dc4e412264f77a7099488b918f74e102b0933'
            'b35f68d687b9eea2e63655b381602d270f3f0fc96adc462eb5affa751e8ddc55'
            'c8506e40722340ca69790ba389885fea37ec43462b09c74d742ba18f1ec53a58'
            'ccfa53fbc020d65a1faecd14e83503702395b5c9ef0a4eb734850ce4c2fb6887'
            'e1d6f771a1669d30398f25874d7cffe37c3fe3fde5aedfff4bfc5c44c4fc9a51'
            '1ad0fd09746998ac73348064a5f7c09346623f0ae426efcac63d5cc2d87c10c2'
            '6a835cf73aed5d5c3bb13f7b3130ba8f65d139b7ae7b36f1a945aeec7303546b'
            'b60376e2d48347b0b3a1ff0abb8daab8cdf2c59f02ce5686831d372b044c53b3'
            '3c93616f948735f8740f5f873b569921ae8dcea1c3c47e457771cdab07f0a4f4'
            'a60c7042d0f001b026b750cf3d2c35bc9b06060e9c9d293edf5a8b8173d96566'
            '6ee450e782d8bcb2e06e338d213f13f1f300598a44bd96f077282de95a7e2475'
            'c9d4519af3399affbe7e0bb2bd9df7f76ceb8850edb8066e626735e4a1e7b8ec'
            '71fb86cac46cc1b681e738856e964a4d32d9efb36ef86b626e762b93c681c52b'
            '305eac77bf3c07dc6314db18000a8a10f26b168e933a909c3399de72bb740b96'
            '7967aa88e24d395c66877dddeee4df098944e67110968535d92c3fae1b133235'
            '1f2416e6bebfd9059c0055959a723c6e26dba72008ae41c4373b85b0c8c329ce'
            '1ffb28fd4e8cb4daba008315e50bacdd9871541f6761b9f7b004a63b6c318327'
            'ffaf6c61a46550d52807d2ed772475f50bb79f192e8d453706b3234f0c59865c'
            'af2ce34d354168eed773e5ea8ed32412dab485d8d206fc491aa07aaab692e1ce'
            'd1073747509668fff40476ecaa859207a7ad718477ff40021f7f7ddf6585203e'
            '22da1b9e6e975f54723b1f86843109c0e46609b3624992e01813e9e0906d5d4b'
            'fbef3b9dc5d3605b415011b52a23ec5f6f775209327652dd6c8e8747358a653b'
            'cf38e2013660f3e3a407a40a333e753f81852cb0b8bb252b5b2b17876f4c6cbb'
            '8f1346184756768d766c3d5cc0eb9aea62ab9156b6a3265bd4892c81c8cc7e1c'
            'c7aa960d4b019c5d90e07bc272beafac14b8c364c6d56c4e690eaf29c89e6dd8'
            '896795ced13831e0c7a05da5d7588472bf9c411944b8b9b0775d303018a4f8a3')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/^GNOME_CONTROL_CENTER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd ${_pkgname}
  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule update

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson ${_pkgname} build -Dcheese=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  echo "X-Purism-FormFactor=Workstation;Mobile;" >> "$pkgdir/usr/share/applications/gnome-control-center.desktop"
}
