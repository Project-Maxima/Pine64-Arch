# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany-mobile
pkgver=40.3
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64 armv7h aarch64)
license=(GPL)
depends=(webkit2gtk gcr icu libdazzle libhandy libportal libarchive)
makedepends=(docbook-xml startup-notification lsb-release gobject-introspection
             yelp-tools git meson)
checkdepends=(xorg-server-xvfb aspell hspell hunspell nuspell libvoikko)
provides=(epiphany)
conflicts=(epiphany)
_commit=39ea7a27f3dc7f6c45095dcb73ce82e78dc0a4b2  # tags/40.3^0
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/epiphany.git#commit=$_commit"
        tests-Disable-create_delete_dir-test.patch
        header-bar-Hide-keyboard-shortcuts-button-on-mobile.patch
        Disable-fullscreen.patch
        firefox-sync-dialog-Use-GtkHeaderBar.patch)
sha256sums=('SKIP'
            '738b7951a2556354685174a3fe17661c65b6c336ec154704a4e0da385c64da76'
            'f2f87379b00183c661f86d2830303ca9d134abdd254dba0e8d82ec6f397f0f4d'
            '5d7b324579d580a90f82a948cf1ce2da7517aac43433dbd724a2f4f9cdb13b29'
            '196c995467367856f0975b3bda03b223e15085422f1c2dce348af6960e5b4120')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
